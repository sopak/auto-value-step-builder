plugins{
    id 'java-library'
    id 'maven'
    id 'signing'
}

sourceCompatibility = vars.sourceCompatibility
targetCompatibility = vars.sourceCompatibility

dependencies {
    // To avoid compiler warnings about @API annotations in JUnit code.
    compile libs.apiGuardian

    api libs.velocity
    api libs.velocityTools
    api libs.autoValue
    api libs.autoCommon
    api libs.autoService
    api libs.jsr305

    api project(':auto-value-step-builder-annotations')

    testCompile libs.junit5
    testCompile libs.junit5Params
    testCompile libs.googleCompileTesting
    testCompile libs.commonsIo
    testCompile files(org.gradle.internal.jvm.Jvm.current().getToolsJar())
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

signing {
    required { isReleaseVersion && gradle.taskGraph.hasTask("uploadArchives") }
    sign configurations.archives
}


uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: System.env.ossrhUsername, password: System.env.ossrhPassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: System.env.ossrhUsername, password: System.env.ossrhPassword)
            }

            pom.project {
                name 'Auto Value Step Builder'
                packaging 'jar'

                description 'AutoValue extension to create step builder pattern for AutoValue properties'
                url 'https://github.com/sopak/auto-value-step-builder'

                scm {
                    connection 'scm:git:https://github.com/sopak/auto-value-step-builder.git'
                    developerConnection 'scm:git:https://github.com/sopak/auto-value-step-builder.git'
                    url 'https://github.com/sopak/auto-value-step-builder'
                }

                licenses {
                    license {
                        name 'The Apache License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers {
                    developer {
                        id 'sopak'
                        name 'Kamil Sopko'
                        email 'kamil.sopko@jcode.cz'
                    }
                }
            }
        }
    }
}